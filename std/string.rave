import <std/memory>

namespace libc {
    // Functions that are practically unnecessary in normal use and come from libc
    extern(linkname: "strlen") int strlen(char* cstr);
    char* btos(bool b) {
        char* toret = "true";
        if(b == false) toret = "false";
    } => toret;
}

namespace std {
    struct string {
        char* ptr;
        int len;

        std::string* this(char* a) {
            this.len = libc::strlen(a);
            //this.ptr = std::malloc(this.len+1);
            this.ptr = std::malloc(this.len);

            int i = 0;
            while(i<this.len) {
                this.ptr[i] = a[i];
                i += 1;
            }
            //this.ptr[i] = '\0';
        } => this;

        bool isDeleted => (this.ptr == null);
        char toChar => this.ptr[0];

        bool has(char c) {
            bool _has = false;

            int i = 0;
            while(i<this.len) {
                if(this.ptr[i] == c) {
                    _has = true;
                    i = this.len;
                }
                i += 1;
            }
        } => _has;

        void replace(char c, char to) {
            int i = 0;
            while(i<this.len) {
                if(this.ptr[i] == c) {
                    this.ptr[i] = to;
                }
                i += 1;
            }
        }

        char* c {
            char* temp = std::malloc(this.len+1);
            int i = 0;
            while(i<this.len) {temp[i] = this.ptr[i]; i += 1;}
            temp[i] = '\0';
            return temp;
        }

        void destroy {
            std::free(this.ptr);
            if(this.ptr != null) this.ptr = null;
        }

        int indexOf(char c) {
            int i = 0;
            int needed = -1;
            while(i<this.len) {if(this.ptr[i] == c) {needed = i; i = this.len;} i += 1;}
        } => needed;

        void appendChar(char c) {
            this.len += 1;
            this.ptr = cast(char*)std::realloc(cast(void*)this.ptr,this.len+1);
            this.ptr[this.len-1] = c;
            this.ptr[this.len] = '\0';
        }
    }
}

macro S {
    return std::string(#0);
}