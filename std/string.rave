import <std/memory>

namespace libc {
    // Functions that are practically unnecessary in normal use and come from libc
    extern(linkname: "strlen") int strlen(char* cstr);
    char* btos(bool b) {
        char* toret = "true";
        if(b == false) toret = "false";
    } => toret;
}

namespace std {
    struct string {
        char* data;
        int length;
        int capacity;

        std::string* this(char* a) {
            this.length = libc::strlen(a);
            this.capacity = this.length*2;
            this.data = std::malloc(this.capacity);

            int i = 0;
            while(i<this.length) {
                this.data[i] = a[i];
                i += 1;
            }
            //this.data[i] = '\0';
        } => this;

        bool isDeleted => (this.data == null);
        char toChar => this.data[0];

        bool has(char c) {
            bool _has = false;

            int i = 0;
            while(i<this.length) {
                if(this.data[i] == c) {
                    _has = true;
                    i = this.length;
                }
                i += 1;
            }
        } => _has;

        void replace(char c, char to) {
            int i = 0;
            while(i<this.length) {
                if(this.data[i] == c) {
                    this.data[i] = to;
                }
                i += 1;
            }
        }

        char* c {
            char* temp = std::malloc(this.length+1);
            int i = 0;
            while(i<this.length) {temp[i] = this.data[i]; i += 1;}
            temp[i] = '\0';
            return temp;
        }

        int indexOf(char c) {
            int i = 0;
            int needed = -1;
            while(i<this.length) {if(this.data[i] == c) {needed = i; i = this.length;} i += 1;}
        } => needed;

        void append(char c) {
            this.length += 1;
            if(this.length == this.capacity) {
                this.capacity = this.capacity * 2;
                this.data = std::realloc(this.data,this.capacity);
            }
            this.data[this.length-1] = c;
        }

        void append(std::string* str) {
            int i = 0;
            while(i<str.length) {
                this.append(str.data[i]);
                i += 1;
            }
        }

        void append(char* cstr) {
            int i = 0;
            while(cstr[i] != '\0') {
                this.append(cstr[i]);
                i += 1;
            }
        }

        void destroy {
            std::free(this.data);
            std::free(this);
        }
    }
}

macro S {
    return std::string(#0);
}