import <std/vector>

namespace libc {
    // Functions that are practically unnecessary in normal use and come from libc
    extern(linkname: "strlen") int strlen(char* cstr);
    char* btos(bool b) {
        char* toret = "true";
        if(b == false) toret = "false";
    } => toret;
    extern(linkname: "strstr") char* strstr(char* src, char* substr);
    extern(linkname: "snprintf") int cformatn(char* str, int size, char* fmt, ...);
    extern(linkname: "sprintf") int cformat(char* str, char* fmt, ...);
    extern(linkname: "strtok") char* strtok(char* str, char* chars);
    extern(linkname: "strtol") long strtol(char* str, char** end, int base);
    (inline) bool isWhitespace(char c) => ((c == ' ') || (c == '\n')) || ((c == '\r') || (c == '\t'));
}

namespace std {
    extern(linkname: "sscanf") int format(char* buff, char* fmt);
    (inline) int _lengthOfNumber(long x) {
        int len = 1;

        if(x >= 1000000000000) len = 13;
        else if(x >= 100000000000) len = 12;
        else if(x >= 10000000000) len = 11;
        else if(x >= 1000000000) len = 10;
        else if(x >= 100000000) len = 9;
        else if(x >= 10000000) len = 8;
        else if(x >= 1000000) len = 7;
        else if(x >= 100000) len = 6;
        else if(x >= 10000) len = 5;
        else if(x >= 1000) len = 4;
        else if(x >= 100) len = 3;
        else if(x >= 10) len = 2;
    } => len;
    (inline) int lengthOfNumber(long x) {
        int len = 0;

        if(x<0) len = std::_lengthOfNumber(-x)+1;
        else len = std::_lengthOfNumber(x);
    } => len;

    struct string {
        char* data;
        int length;
        int capacity;

        std::string this(char* a) {
            if(a != null) {
                this.length = libc::strlen(a);
                this.capacity = this.length*2;
                if(this.capacity == 0) this.capacity = 1;
                this.data = std::malloc(this.capacity);

                int i = 0;
                while(i<this.length) {
                    this.data[i] = a[i];
                    i += 1;
                }
            }
            else {
                this.length = 0;
                this.capacity = 1;
                this.data = std::malloc(this.capacity);
            }
        } => this;

        bool isDeleted => (this.data == null);

        bool has(char c) {
            bool _has = false;

            int i = 0;
            while(i<this.length) {
                if(this.data[i] == c) {
                    _has = true;
                    i = this.length;
                }
                i += 1;
            }
        } => _has;

        char* c {
            char* temp = std::malloc(this.length+1);
            int i = 0;
            while(i<this.length) {temp[i] = this.data[i]; i += 1;}
            temp[i] = '\0';
        } => temp;

        (inline) char toChar => this.data[0];
        (inline) long toLong => libc::strtol(this.c(),cast(char**)null,10);
        (inline) int toInt => cast(int)this.toLong();

        (inline) std::string fromNumber(long n) {
            char* buf = std::malloc(std::lengthOfNumber(n)+2);
            libc::cformat(buf,"%d",n);
        } => std::string(buf);
        (inline) std::string fromNumber(int n) => this.fromNumber(cast(long)n);
        (inline) std::string fromNumber(short n) => this.fromNumber(cast(long)n);
        (inline) std::string fromNumber(char n) => this.fromNumber(cast(long)n);
        (inline) std::string fromNumber(bool n) => this.fromNumber(cast(long)n);
        (inline) std::string fromFloat(float n) {
            char* buf = std::malloc(128);
            libc::cformat(buf,"%f",cast(double)n);
        } => std::string(buf);
        (inline) std::string fromDouble(double n) {
            char* buf = std::malloc(128);
            libc::cformat(buf,"%f",n);
        } => std::string(buf);

        int indexOf(char c) {
            int i = 0;
            int needed = -1;
            while(i<this.length) {if(this.data[i] == c) {needed = i; i = this.length;} i += 1;}
        } => needed;

        int indexOf(char* s) {
            char* _c = this.c();
            char* p = libc::strstr(_c,s);

            int index = 0;
            if(p == null) index = -1;
            else index = (cast(int)p)-(cast(int)_c);
        } => index;

        int indexOf(std::string s) => this.indexOf(s.c());

        void append(char c) {
            this.length += 1;
            if(this.length == this.capacity) {
                this.capacity = this.capacity * 2;
                this.data = std::realloc(this.data,this.capacity);
            }
            this.data[this.length-1] = c;
        }

        void append(std::string str) {
            int i = 0;
            while(i<str.length) {
                this.append(str.data[i]);
                i += 1;
            }
        }

        void append(char* cstr) {
            for(int i=0; cstr[i] != '\0'; i+=1) {
                this.append(cstr[i]);
            }
        }

        std::string copy {
            std::string cp = std::string("");

            int i = 0;
            cp.append(*this);
        } => cp;

        std::string via(char c) {
            std::string _cp = this.copy();
            _cp.append(c);
        } => _cp;

        std::string via(std::string s) {
            std::string _cp = this.copy();
            _cp.append(s);
        } => _cp;

        std::string via(char* s) {
            std::string _cp = this.copy();
            _cp.append(s);
        } => _cp;

        std::string replace(char c, char to) {
            std::string temp = this.copy();

            int i = 0;
            while(i<this.length) {
                if(this.data[i] == c) {
                    temp.data[i] = to;
                }
                i += 1;
            }
        } => temp;

        std::string operator+(std::string one, std::string two) {
            std::string third = std::string(one.c());
            third.append(two);
        } => third;

        bool compare(std::string another) {
            bool isEqual = true;
            if(this.length != another.length) {
                isEqual = false;
            }
            else {
                int i = 0;
                while(i<another.length) {
                    if(this.data[i] != another.data[i]) isEqual = false;
                    i += 1;
                }
            }
        } => isEqual;

        bool compare(char* another) {
            bool isEqual = true;
            int len = libc::strlen(another);

            if(this.length != len) {
                isEqual = false;
            }
            else {
                int i = 0;
                while(i<len) {
                    if(this.data[i] != another[i]) isEqual = false;
                    i += 1;
                }
            }    
        } => isEqual;

        bool operator==(std::string one, std::string two) => one.compare(two);
        bool operator==(std::string one, char* two) => one.compare(two);
        bool operator!=(std::string one, std::string two) => !(one.compare(two));
        bool operator!=(std::string one, char* two) => !(one.compare(two));
        void operator=(std::string* one, char* two) {
            one[0] = std::string(two);
        }

        std::string substring(int from, int to) {
            std::string temp = "";

            int i = from;
            while(i<to) {
                temp.append(this.data[i]);
                i += 1;
            }
        } => temp;

        std::vector<std::string> splitByChars(char* chars) {
            std::vector<std::string> parts = std::vector<std::string>();

            char* _c = this.c();
            char* _cp = libc::strtok(_c,chars);

            while(_cp != null) {
                parts.add(std::string(_cp));
                _cp = libc::strtok(null,chars);
            }

            std::free(_c);
        } => parts;

        std::string ltrim {
            char* _c = this.c();

            while(libc::isWhitespace(_c[0])) _c = cast(char*)((cast(int)_c) + 1);

            std::string _s = std::string(_c);
        } => _s;

        std::string rtrim {
            char* _c = this.c();

            int i = libc::strlen(_c)-1;
            while(libc::isWhitespace(_c[i])) i -= 1;
            _c[i+1] = '\0';

            std::string _s = std::string(_c);
        } => _s;

        std::string trim {
            std::string _l = this.ltrim();
        } => _l.rtrim();

        (inline) std::vector<std::string> splitByChars(std::string chars) => this.splitByChars(chars.c());

        void ~this {
            std::free(this.data);
        }
    }

    (ctargs) std::string sprint {
        std::string result = "";
        std::vector<void*> values = std::vector<void*>();
        std::vector<char*> types = std::vector<char*>();

        int z = 0;
        @foreachArgs() {
            @if(@typesIsNequals(@getCurrArgType(),float)) {
                @if(@typesIsNequals(@getCurrArgType(),double)) {
                    @if(@typesIsNequals(@getCurrArgType(),std::string)) {
                        /*@if(@typesIsEquals(@getCurrArgType(),std::ustring)) {
                            std::ustring us = @getCurrArg(std::ustring);
                            values.add(cast(void*)&us);
                            types.add("std::ustring*");
                        };*/
                        //@if(@typesIsNequals(@getCurrArgType(),std::ustring)) {
                            values.add(@getCurrArg(void*));
                            types.add(@typeToString(@getCurrArgType()));
                        //};
                    };
                    @if(@typesIsEquals(@getCurrArgType(),std::string)) {
                        std::string s = @getCurrArg(std::string);
                        values.add(cast(void*)&s);
                        types.add("std::string*");
                    };
                };
                @if(@typesIsEquals(@getCurrArgType(),double)) {
                    double d = @getCurrArg(double);
                    values.add(cast(void*)&d);
                    types.add("double");
                };
            };
            @if(@typesIsEquals(@getCurrArgType(),float)) {
                double f = @getCurrArg(double);
                values.add(cast(void*)&f);
                types.add("double");
            };
            z += 1;
        };

        int i = 0;
        while(i<types.length) {
            if(types.data[i] == "char*") {
                result.append(std::string(values.data[i]));
            }
            else if((types.data[i] == "std::string") || (types.data[i] == "std::string*")) {
                std::string* s = cast(std::string*)values.data[i];
                result.append(s);
            }
            else if((types.data[i] == "float") || (types.data[i] == "double")) {
                double* d = cast(double*)values.data[i];
                result.append(result.fromDouble(d[0]));
            }
            else if((types.data[i] == "long" || types.data[i] == "int") || types.data[i] == "short") {
                long l = cast(long)values.data[i];
                result.append(result.fromNumber(l));
            }
            else if(types.data[i] == "bool") {
                bool b = cast(bool)values.data[i];
                if(b) result.append(std::string("true"));
                else result.append(std::string("false"));
            }
            else if(types.data[i] == "char") {
                result.append(cast(char)values.data[i]);
            }
            else if(types.data[i] == "uchar" || types.data[i] == "uint" || types.data[i] == "ushort" || types.data[i] == "ulong") {
                char[512] _cs;
                ulong ul = cast(ulong)values.data[i];
                libc::cformat(&_cs, "%lu", cast(ulong)ul);
                result.append(&_cs);
            }
            i += 1;
        }
    } => result;
}

macro S {
    return std::string(#0);
}